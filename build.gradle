plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.mockgen'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-csv:1.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

application {
    mainClass = 'com.mockgen.Main'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.mockgen.Main'
    }

    // Делаем fat jar
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    archiveBaseName.set('mockgen')
    archiveClassifier.set('')
    archiveVersion.set(version)
    manifest {
        attributes 'Main-Class': 'com.mockgen.Main'
    }
}

tasks.distZip.enabled = false
tasks.distTar.enabled = false
tasks.startScripts.enabled = false

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'BUNDLE'
            includes = ['com.mockgen.*']
            excludes = [
                    'com.mockgen.service.ReportService',
                    'com.mockgen.config.*',
                    'com.mockgen.Main'
            ]
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification